import groovy.json.JsonSlurper
import java.util.Base64
import jenkins.model.Jenkins
import com.cloudbees.plugins.credentials.CredentialsProvider
import com.cloudbees.plugins.credentials.common.StandardUsernamePasswordCredentials

// ====== PARAMS ======
def app       = "eai-camel-rgv"
def groupPath = "fr/labanquepostale/marches/eai"

// repo Artifactory (à adapter selon release/snapshot)
def repo      = "libs-release-local"

// base url: idéalement "https://.../artifactory"
def baseUrl   = System.getenv("EAI_ARTIFACTORY_URL") ?: "https://dlu.pop.sf.intra.laposte.fr/artifactory"

// Credential Jenkins (username/password ou user/token)
def CRED_ID   = "usr_gitlab_eai"
// ====================

if (!app || !baseUrl || !repo || !groupPath) return ["(missing params)"]

// Normalisation baseUrl: si on te donne https://host/ on ajoute /artifactory
baseUrl = baseUrl.trim()
if (baseUrl.endsWith("/")) baseUrl = baseUrl[0..-2]
if (!baseUrl.contains("/artifactory")) baseUrl = baseUrl + "/artifactory"

// Récup credentials
def creds = CredentialsProvider.lookupCredentials(
  StandardUsernamePasswordCredentials,
  Jenkins.instance,
  null,
  null
).find { it.id == CRED_ID }

if (!creds) return ["(credential not found: ${CRED_ID})"]

def user  = creds.username
def token = creds.password?.plainText
if (!user || !token) return ["(empty credential)"]

// AQL: on liste tous les fichiers jar sous group/app/<version>/
def aql = """
items.find({
  "repo": "${repo}",
  "path": {"\\\$match":"${groupPath}/${app}/*"},
  "name": {"\\\$match":"${app}-*.jar"}
}).include("path","name")
"""

def url = new URL("${baseUrl}/api/search/aql")
def conn = (HttpURLConnection) url.openConnection()
conn.setRequestMethod("POST")
conn.setDoOutput(true)
conn.setConnectTimeout(5000)
conn.setReadTimeout(15000)

def auth = Base64.encoder.encodeToString("${user}:${token}".getBytes("UTF-8"))
conn.setRequestProperty("Authorization", "Basic ${auth}")
conn.setRequestProperty("Content-Type", "text/plain; charset=UTF-8")

conn.outputStream.withWriter("UTF-8") { it << aql }

def rc = conn.responseCode
def stream = (rc >= 200 && rc < 300) ? conn.inputStream : conn.errorStream
def payload = stream ? stream.getText("UTF-8") : ""

if (!(rc >= 200 && rc < 300)) {
  return ["(Artifactory HTTP ${rc}) ${payload?.take(200) ?: ''}"]
}

def json = new JsonSlurper().parseText(payload)

// Extraction de version depuis le path: group/app/<version>
def prefix = "${groupPath}/${app}/"

def versions = (json?.results ?: []).collect { r ->
  def p = r.path as String
  if (!p) return null
  if (!p.startsWith(prefix)) return null
  def rest = p.substring(prefix.length())
  def v = rest.tokenize('/')[0]
  return v
}.findAll { it }.unique()

return versions ? versions.sort().reverse() : ["(no versions found)"]
